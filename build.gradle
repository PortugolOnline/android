apply plugin: 'com.android.application'

// Assinatura do APK: requisito para publicação na Google Play Store
// https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
// https://stackoverflow.com/a/43230396/1657502

// Antes de assinar, gerar a chave privada:
// $ keytool -genkey -v -keystore ~/portugol.jks -keyalg RSA -keysize 2048 -validity 10000 -alias portugol

// E criar um arquivo denominado keystore.properties no diretório raiz do projeto. Esse arquivo deve
// conter as informações de assinatura, da seguinte forma:

// storePassword=myStorePassword
// keyPassword=mykeyPassword
// keyAlias=myKeyAlias
// storeFile=myStoreFileLocation

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "br.com.vinyanalista.portugol.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 4
        versionName "0.3.0-beta"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", applicationId + "-" + versionName)

        // https://developer.android.com/studio/build/gradle-tips#share-properties-with-the-manifest
        def filesAuthorityValue = applicationId + ".files"
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""

        def documentsAuthorityValue = applicationId + ".documents"
        buildConfigField "String", "DOCUMENTS_AUTHORITY", "\"${documentsAuthorityValue}\""

        // https://stackoverflow.com/a/50860943/1657502
        manifestPlaceholders = [documentsAuthority: documentsAuthorityValue,
                                filesAuthority: filesAuthorityValue]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:design:27.1.1'
    implementation project(':interpretador')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
